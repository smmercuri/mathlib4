/-
Copyright (c) 2024 Salvatore Mercuri, MarÃ­a InÃ©s de Frutos-FernÃ¡ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Salvatore Mercuri, MarÃ­a InÃ©s de Frutos-FernÃ¡ndez
-/
module

public import Mathlib.NumberTheory.NumberField.InfiniteAdeleRing
public import Mathlib.RingTheory.DedekindDomain.FiniteAdeleRing
public import Mathlib.NumberTheory.NumberField.ProductFormula
public import Mathlib.Algebra.Group.Pi.Units

/-!
# The adele ring of a number field

This file contains the formalisation of the adele ring of a number field as the
direct product of the infinite adele ring and the finite adele ring.

## Main definitions

- `NumberField.AdeleRing K` is the adele ring of a number field `K`.
- `NumberField.AdeleRing.principalSubgroup K` is the subgroup of principal adeles `(x)áµ¥`.

## References
* [J.W.S. Cassels, A. FrÃ¶hlich, *Algebraic Number Theory*][cassels1967algebraic]

## Tags
adele ring, number field
-/

@[expose] public section

noncomputable section

namespace NumberField

open InfinitePlace AbsoluteValue.Completion InfinitePlace.Completion IsDedekindDomain

/-! ## The adele ring  -/

/-- `AdeleRing (ğ“ K) K` is the adele ring of a number field `K`.

More generally `AdeleRing R K` can be used if `K` is the field of fractions
of the Dedekind domain `R`. This enables use of rings like `AdeleRing â„¤ â„š`, which
in practice are easier to work with than `AdeleRing (ğ“ â„š) â„š`.

Note that this definition does not give the correct answer in the function field case.
-/
def AdeleRing (R K : Type*) [CommRing R] [IsDedekindDomain R] [Field K]
    [Algebra R K] [IsFractionRing R K] := InfiniteAdeleRing K Ã— FiniteAdeleRing R K

namespace AdeleRing

variable (R K : Type*) [CommRing R] [IsDedekindDomain R] [Field K]
  [Algebra R K] [IsFractionRing R K]

instance : CommRing (AdeleRing R K) := Prod.instCommRing

instance : Inhabited (AdeleRing R K) := âŸ¨0âŸ©

instance : TopologicalSpace (AdeleRing R K) := instTopologicalSpaceProd

instance : IsTopologicalRing (AdeleRing R K) := instIsTopologicalRingProd

instance : Algebra K (AdeleRing R K) := Prod.algebra _ _ _

@[simp]
theorem algebraMap_fst_apply (x : K) (v : InfinitePlace K) :
    (algebraMap K (AdeleRing R K) x).1 v = x := rfl

theorem algebraMap_fst_def (x : K) :
    (algebraMap K (AdeleRing R K) x).1 = algebraMap K (InfiniteAdeleRing K) x := rfl

@[simp]
theorem algebraMap_snd_apply (x : K) (v : HeightOneSpectrum R) :
    (algebraMap K (AdeleRing R K) x).2 v = x := rfl

theorem algebraMap_snd_def (x : K) :
    (algebraMap K (AdeleRing R K) x).2 = algebraMap K (FiniteAdeleRing R K) x := rfl

theorem algebraMap_injective [NumberField K] : Function.Injective (algebraMap K (AdeleRing R K)) :=
  fun _ _ hxy => (algebraMap K _).injective (Prod.ext_iff.1 hxy).1

/-- The subgroup of principal adeles `(x)áµ¥` where `x âˆˆ K`. -/
abbrev principalSubgroup : AddSubgroup (AdeleRing R K) := (algebraMap K _).range.toAddSubgroup

end AdeleRing

def IdeleGroup (R K : Type) [CommRing R] [IsDedekindDomain R] [Field K] [Algebra R K]
    [IsFractionRing R K] : Type := (AdeleRing R K)Ë£

section norm

variable {K : Type*} [Field K] [NumberField K]

namespace FiniteAdeleRing

open RingOfIntegers.HeightOneSpectrum

-- not true! the product formula is meant to be for the ideles
theorem mulSupport_finite {x : (FiniteAdeleRing (ğ“ K) K)Ë£} :
    (Function.mulSupport fun v â†¦ â€–x.1 vâ€–).Finite := by
  have := FiniteAdeleRing.isUnit_iff.1 x.isUnit
  obtain h := this.2
  simp at h
  simp [instNormedFieldValuedAdicCompletion, Valued.toNormedField]
  simp_rw [Valued.norm_def]
  change (Function.mulSupport fun v â†¦ (WithZeroMulInt.toNNReal (absNorm_ne_zero v) (Valued.v (x.1 v)) : â„)).Finite
  convert h
  ext v
  simp
  apply Iff.not
  constructor
  Â· intro h
    rw [map_eq_one_iff] at h
    exact h
    exact WithZeroMulInt.toNNReal_strictMono (one_lt_absNorm_nnreal v) |>.injective
  Â· rintro h
    simp_rw [h]
    simp

-- not realy defined outside the units, gets junk value of 1
instance : Norm (FiniteAdeleRing (ğ“ K) K)Ë£ where norm x := âˆá¶  v, â€–x.1 vâ€–

theorem norm_def (x : (FiniteAdeleRing (ğ“ K) K)Ë£) : â€–xâ€– = âˆá¶  v, â€–x.1 vâ€– := rfl

theorem coe_norm_apply (x : KË£) :
    â€–(x : (FiniteAdeleRing (ğ“ K) K)Ë£)â€– = âˆá¶  v, FinitePlace.mk v x.1 := rfl

theorem coe_norm_apply_eq_finprod_finitePlace (x : KË£) :
    â€–(x : (FiniteAdeleRing (ğ“ K) K)Ë£)â€– = âˆá¶  v : FinitePlace K, v x := by
  rw [coe_norm_apply, â† finprod_comp FinitePlace.equivHeightOneSpectrum.invFun
    FinitePlace.equivHeightOneSpectrum.symm.bijective]
  exact finprod_congr fun _ â†¦ rfl

theorem coe_norm_eq_inv_abs_norm (x : KË£) :
    â€–(x : (FiniteAdeleRing (ğ“ K) K)Ë£)â€– = |Algebra.norm â„š x.1|â»Â¹ := by
  rw [â† FinitePlace.prod_eq_inv_abs_norm x.ne_zero, coe_norm_apply_eq_finprod_finitePlace]

end FiniteAdeleRing

namespace AdeleRing

theorem isUnit_iff {x : AdeleRing (ğ“ K) K} :
    IsUnit x â†” (âˆ€ v, x.1 v â‰  0) âˆ§ (âˆ€ v, x.2 v â‰  0) âˆ§
      âˆ€á¶  v in Filter.cofinite, Valued.v (x.2 v) = 1 := by
  rw [Prod.isUnit_iff, Pi.isUnit_iff, FiniteAdeleRing.isUnit_iff]
  simp_rw [isUnit_iff_ne_zero]

instance : Norm (AdeleRing (ğ“ K) K)Ë£ where norm x := â€–x.1.1â€– * â€–(MulEquiv.prodUnits x).2â€–

theorem norm_def (x : (AdeleRing (ğ“ K) K)Ë£) : â€–xâ€– = â€–x.1.1â€– * â€–(MulEquiv.prodUnits x).2â€– := rfl

theorem norm_apply (x : (AdeleRing (ğ“ K) K)Ë£) :
    â€–xâ€– = (âˆ v, â€–x.1.1 vâ€– ^ v.mult) * âˆá¶  v, â€–(MulEquiv.prodUnits x).2.1 vâ€– := rfl

variable (K) in
def unitEmbedding : KË£ â†’* (AdeleRing (ğ“ K) K)Ë£ := Units.map (algebraMap K (AdeleRing (ğ“ K) K))

@[simp] theorem unitEmbedding_apply (k : KË£) :
    unitEmbedding K k = algebraMap K (AdeleRing (ğ“ K) K) k := rfl

theorem unitEmbedding_prodUnits_apply (k : KË£) :
    (MulEquiv.prodUnits (unitEmbedding K k)).2 = k := rfl

instance : Coe KË£ (AdeleRing (ğ“ K) K)Ë£ where
  coe x := unitEmbedding K x

theorem coe_norm_eq_one {x : KË£} :
    â€–(x : (AdeleRing (ğ“ K) K)Ë£)â€– = 1 := by
  rw [norm_def, unitEmbedding_apply, algebraMap_fst_def]
  rw [unitEmbedding_prodUnits_apply, InfiniteAdeleRing.coe_norm_eq_abs_norm,
    FiniteAdeleRing.coe_norm_eq_inv_abs_norm x]
  simp

end AdeleRing

end norm

end NumberField
