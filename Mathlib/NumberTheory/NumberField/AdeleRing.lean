/-
Copyright (c) 2024 Salvatore Mercuri, MarÃ­a InÃ©s de Frutos-FernÃ¡ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Salvatore Mercuri, MarÃ­a InÃ©s de Frutos-FernÃ¡ndez
-/
module

public import Mathlib.NumberTheory.NumberField.InfiniteAdeleRing
public import Mathlib.RingTheory.DedekindDomain.FiniteAdeleRing
public import Mathlib.NumberTheory.NumberField.ProductFormula

/-!
# The adele ring of a number field

This file contains the formalisation of the adele ring of a number field as the
direct product of the infinite adele ring and the finite adele ring.

## Main definitions

- `NumberField.AdeleRing K` is the adele ring of a number field `K`.
- `NumberField.AdeleRing.principalSubgroup K` is the subgroup of principal adeles `(x)áµ¥`.

## References
* [J.W.S. Cassels, A. FrÃ¶hlich, *Algebraic Number Theory*][cassels1967algebraic]

## Tags
adele ring, number field
-/

@[expose] public section

noncomputable section

namespace NumberField

open InfinitePlace AbsoluteValue.Completion InfinitePlace.Completion IsDedekindDomain

/-! ## The adele ring  -/

/-- `AdeleRing (ğ“ K) K` is the adele ring of a number field `K`.

More generally `AdeleRing R K` can be used if `K` is the field of fractions
of the Dedekind domain `R`. This enables use of rings like `AdeleRing â„¤ â„š`, which
in practice are easier to work with than `AdeleRing (ğ“ â„š) â„š`.

Note that this definition does not give the correct answer in the function field case.
-/
def AdeleRing (R K : Type*) [CommRing R] [IsDedekindDomain R] [Field K]
    [Algebra R K] [IsFractionRing R K] := InfiniteAdeleRing K Ã— FiniteAdeleRing R K

namespace AdeleRing

variable (R K : Type*) [CommRing R] [IsDedekindDomain R] [Field K]
  [Algebra R K] [IsFractionRing R K]

instance : CommRing (AdeleRing R K) := Prod.instCommRing

instance : Inhabited (AdeleRing R K) := âŸ¨0âŸ©

instance : TopologicalSpace (AdeleRing R K) := instTopologicalSpaceProd

instance : IsTopologicalRing (AdeleRing R K) := instIsTopologicalRingProd

instance : Algebra K (AdeleRing R K) := Prod.algebra _ _ _

@[simp]
theorem algebraMap_fst_apply (x : K) (v : InfinitePlace K) :
    (algebraMap K (AdeleRing R K) x).1 v = x := rfl

theorem algebraMap_fst_def (x : K) :
    (algebraMap K (AdeleRing R K) x).1 = algebraMap K (InfiniteAdeleRing K) x := rfl

@[simp]
theorem algebraMap_snd_apply (x : K) (v : HeightOneSpectrum R) :
    (algebraMap K (AdeleRing R K) x).2 v = x := rfl

theorem algebraMap_snd_def (x : K) :
    (algebraMap K (AdeleRing R K) x).2 = algebraMap K (FiniteAdeleRing R K) x := rfl

theorem algebraMap_injective [NumberField K] : Function.Injective (algebraMap K (AdeleRing R K)) :=
  fun _ _ hxy => (algebraMap K _).injective (Prod.ext_iff.1 hxy).1

/-- The subgroup of principal adeles `(x)áµ¥` where `x âˆˆ K`. -/
abbrev principalSubgroup : AddSubgroup (AdeleRing R K) := (algebraMap K _).range.toAddSubgroup

end AdeleRing

section norm

variable {K : Type*} [Field K] [NumberField K]

namespace FiniteAdeleRing

instance : Norm (FiniteAdeleRing (ğ“ K) K) where
  norm x := âˆá¶  v, â€–x vâ€–

theorem norm_apply (x : FiniteAdeleRing (ğ“ K) K) : â€–xâ€– = âˆá¶  v, â€–x vâ€– := rfl

theorem coe_norm_apply (x : K) :
    â€–algebraMap _ (FiniteAdeleRing (ğ“ K) K) xâ€– = âˆá¶  v, FinitePlace.mk v x := rfl

theorem coe_norm_apply_eq_finprod_finitePlace (x : K) :
    â€–algebraMap _ (FiniteAdeleRing (ğ“ K) K) xâ€– = âˆá¶  v : FinitePlace K, v x := by
  rw [coe_norm_apply, â† finprod_comp FinitePlace.equivHeightOneSpectrum.invFun
    FinitePlace.equivHeightOneSpectrum.symm.bijective]
  exact finprod_congr fun _ â†¦ rfl

theorem coe_norm_eq_inv_abs_norm {x : K} (hx : x â‰  0) :
    â€–algebraMap _ (FiniteAdeleRing (ğ“ K) K) xâ€– = |Algebra.norm â„š x|â»Â¹ := by
  rw [â† FinitePlace.prod_eq_inv_abs_norm hx, coe_norm_apply_eq_finprod_finitePlace]

end FiniteAdeleRing

namespace AdeleRing

instance : Norm (AdeleRing (ğ“ K) K) where norm x := â€–x.1â€– * â€–x.2â€–

theorem norm_def (x : AdeleRing (ğ“ K) K) : â€–xâ€– = â€–x.1â€– * â€–x.2â€– := rfl

theorem norm_apply (x : AdeleRing (ğ“ K) K) : â€–xâ€– = (âˆ v, â€–x.1 vâ€– ^ v.mult) * âˆá¶  v, â€–x.2 vâ€– := rfl

theorem coe_norm_eq_one {x : K} (hx : x â‰  0) :
    â€–algebraMap _ (AdeleRing (ğ“ K) K) xâ€– = 1 := by
  simp [norm_def, algebraMap_fst_def, InfiniteAdeleRing.norm_apply, algebraMap_snd_def,
    FiniteAdeleRing.coe_norm_apply_eq_finprod_finitePlace]
  rw [â† prod_abs_eq_one hx]
  rfl

end AdeleRing

end norm

end NumberField
